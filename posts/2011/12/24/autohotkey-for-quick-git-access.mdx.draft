---
title: "AutoHotKey for quick Git access  #Programming  #autohotkey  #git"
sort: 3370
---
I really like <a href="http://git-scm.com/">Git</a>'s GUI tools: Git Gui for reviewing diffs and committing, and Gitk for dealing with branches. They're powerful tools -- powerful enough that I'm skeptical of any IDE that claims to include all that functionality in a little tool window.

So that means I do a lot of task switching between my IDE, Git Gui, and Gitk. And since I hate repetitive Alt+Tabbing, I've written an <a href="http://www.autohotkey.com/">AutoHotKey</a> script to make it easier.

AutoHotKey is an open-source tool that lets you bind scripts to various keys or key combinations on your keyboard. I've chosen these keybindings:

<ul>
<li><strong>Numpad Minus:</strong> bring up the <strong>Git Gui</strong> window (or start it if it's not already running), send F5 to refresh it, and put focus in the "description" box.</li>
<li><strong>Numpad Plus:</strong> bring up the <strong>Gitk</strong> window (or start it if it's not already running) and send F5 to refresh it. (Mnemonic: Gitk shows a tree, hence my choice of "+")</li>
</ul>



I wrote a little AutoHotKey script and put it in version control at work, and several of the other devs started using it too. It's almost disorienting when I'm at someone else's computer and they don't have it running.

Here's the script. I suggest saving it as a <code>GitHotKeys.ahk</code> file somewhere inside your Git working copy; that way, if it needs to launch Git Gui or Gitk, they'll automatically come up in the right repository.

<pre>
; Copyright (c) 2011 Joe White
;
; Permission is hereby granted, free of charge, to any person obtaining
; a copy of this software and associated documentation files (the
; "Software"), to deal in the Software without restriction, including
; without limitation the rights to use, copy, modify, merge, publish,
; distribute, sublicense, and/or sell copies of the Software, and to
; permit persons to whom the Software is furnished to do so, subject to
; the following conditions:
;
; The above copyright notice and this permission notice shall be
; included in all copies or substantial portions of the Software.
;
; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
; OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
; FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
; AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
; LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
; OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
; SOFTWARE.

#NoEnv  ; Recommended for performance and compatibility with future AutoHotkey releases.
SendMode Input  ; Recommended for new scripts due to its superior speed and reliability.
SetWorkingDir %A_ScriptDir%  ; Ensures a consistent starting directory.
SetTitleMatchMode, 1

NumpadSub::
IfWinNotExist, Git Gui
  Run, git gui,, Hide
WinActivate, Git Gui
IfWinActive, Git Gui
{
  ControlGetPos, control_x, control_y,,, TkChild18, Git Gui
  CoordMode, Mouse, Screen
  MouseGetPos, mouse_x, mouse_y
  click_x := control_x + 5
  click_y := control_y + 5
  CoordMode, Mouse, Relative
  Click %click_x%, %click_y%
  CoordMode, Mouse, Screen
  MouseMove, %mouse_x%, %mouse_y%, 0

  Send, {F5}
}
return

NumpadAdd::
IfWinNotExist, gitk
  Run, gitk
WinActivate, gitk
IfWinActive, gitk
  Send, {F5}
return
</pre>
